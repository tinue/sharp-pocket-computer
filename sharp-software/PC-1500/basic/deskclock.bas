10: "A"ARUN :CLEAR :T1=TIME :BEEP OFF :LOCK :REM Desk-Clock
20: DIM WT$(7)*2:WT$(1)="MO":WT$(2)="TU":WT$(3)="WE":WT$(4)="TH":WT$(5)="FR"
30: BE$="*": AT$="*"
40: "UHR"WT$(6)="SA":WT$(7)="SU"
50: T2=TIME
60: IF WS=74AND T2-T1>WT PRINT "Auto Power OFF ! (ENTER)":CALL &E33F:WAIT 0:T1=TIME :T2=0:GOTO "UHR"
70: JR$="24": REM Current Year
80: TT=TIME *10^4:TT$=STR$ TT:IF LEN TT$<10LET TT$="0"+TT$
90: JJ=VAL LEFT$(TT$,2):DD=JJ
100: DD$=CHR$ &7F+MID$ (TT$,3,2)+"."+LEFT$(TT$,2)+"."+JR$+CHR$ &7F+BE$+CHR$ &7F
110: NN=VAL MID$ (TT$,3,2)
120: WAIT 3.8:OO=JJ:PP=VAL JR$+2000:OO=OO+13:IF OO>15LET OO=OO-12:PP=PP+1
130: VV=INT (1461*(PP-1)/4)+INT (153*OO/5)+NN-621050:II=VV-INT (VV/7)*7+1
140: ZZ$=ZZ$+CHR$ &7F
150: PRINT DD$+WT$(II)+ZZ$
160: IF VAL MID$ (TT$,7,2)=59AND VAL RIGHT$ (TT$,2)>=56GOTO "TON"
170: ZZ$=CHR$ &7F+MID$ (TT$,5,2)+":"+MID$ (TT$,7,2)+":"+RIGHT$ (TT$,2)+CHR$ &7F+AT$+CHR$ &7F
180: VT$=MID$ (ZZ$,3,5)
190: A=ASC INKEY$
200: IF A=10GOTO "END"
210: IF A=66GOTO "BON"
220: IF A=83GOTO "STELLEN"
230: IF A=65GOTO "WARTEN"
240: GOTO "UHR"
250: "STELLEN"CLS :BEEP 1:WAIT 0:PRINT "Set time (Y/N)?"
260: "STELLEN1"A=ASC INKEY$
270: IF A=89GOTO "ST2"
280: IF A=78GOTO "UHR"
290: GOTO "STELLEN1"
300: "ST2": BEEP 1:WAIT 0: PRINT CHR$ &7F;"Hour:  ";CHR$ &7F;"Min.:  ";CHR$ &7F; "Sec.:  ";CHR$ &7F;CHR$ &7F
310: "ST"BEEP 1:CURSOR 6: INPUT ST$
320: ST=VAL ST$: IFST >23OR ST<0BEEP 3: GOTO "ST"
330: "MIN"BEEP 1:CURSOR 14:INPUT MI$
340: MI=VAL MI$: IFMI<0OR MI >59BEEP 3: GOTO "MI"
350: "SEC"BEEP 1:CURSOR 22:INPUT SE$
360: SE=VAL SE$: IF SE<0OR SE>59BEEP 3: GOTO "SEC"
370: CLS :BEEP 1:WAIT 0:CURSOR 3: PRINT CHR$ & 7F; "Day:  ";CHR$ &7F; "Month:  ";CHR$ &7F;CHR$ &7F
380: "TAG"BEEP 1:CURSOR 8: INPUTTA$
390: TA=VAL TA$: IFTA>31OR TA<1BEEP 3: GOTO "TAG"
400: "MONAT"BEEP 1:CURSOR 17:INPUT MO$
410: MO=VAL MO$: IF MO>12OR MO<1BEEP 3: GOTO "MONAT"
420: TIME =VAL (MO$+TA$+ST$+". "+MI$+SE$)
430: CLS :T1=TIME :GOTO "UHR"
440: "WARTEN" CLS :BEEP 1:WAIT 0:PRINT "Auto power off (Y/N) ?"
450: "WRT"WS=ASC INKEY$
460: IF WS=89LET AT$="A": GOTO "WRT1"
470: IF WS=78LET AT$="*": GOTO "UHR"
480: GOTO "WRT"
490: "WRT1"CLS :WAIT 0:BEEP 1:PRINT "Power off Minutes:"
500: "WRT2"CURSOR 19:INPUT WT:WT=WT/100:IF WT<0.01OR WT>0.3BEEP 3:GOTO "WRT2"
510: T2=TIME :CLS :GOTO "UHR"
520: "TON"WAIT 0:BEEP 3, 150, 250: BEEP 1, 255, 350: GOTO "UHR"
530: "BON"BEEP 1:WAIT 0:PRINT "BEEP O)n/Of)f?"
540: "BON1"A=ASC INKEY$
550: IF A=79BEEP ON :BE$="B": GOTO"UHR"
560: IF A=70BEEP OFF :BE$="*":GOTO "UHR"
570: GOTO "BON1"
580: "END"UNLOCK:END